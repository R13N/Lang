Types

author		"Pieter Vander Vennet"
date		[2014, 06, 7, 18, 24, 42]
desc		"The syntaxis of types in Languate"

public import Prelude

_knownType	::= globalIdent
_freeType	::= localIdent
_void		::= "\(\)"
_simpleType	::= knownType | freeType | void | "\(" type "\)"


_commaSepTypes	::= type ( "," type)*
_list		::= "\[" commaSepTypes "\]"
_set		::= "\{" commaSepTypes "\}"
_dict		::= "\{" commaSepTypes "-->" commaSepTypes "\}"

_tuple		::= "\(" type ("," type)+ "\)"

_baseType	::= dict | set | list | tuple | simpleType

_curry		::= baseType ("->" baseType)+

type		::= curry | baseType
